import java.util.regex.Pattern

apply plugin: 'com.android.library'
apply plugin: 'maven'

apply from: project.file("version.gradle")

ext.pomProjectName = 'Atlas library'
ext.artifactIdName = 'layer-atlas'
group 'com.layer.ui'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionName = "${version}"

        testInstrumentationRunner "com.layer.ui.TestButlerRunner"
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
}

repositories {
    maven { url "https://raw.githubusercontent.com/layerhq/releases-android/master/releases/" }
    jcenter()
}

ext {
    org = "layerhq"
    repo = "Atlas-Android"
    groupFolder = "com/layer/atlas"
}

dependencies {
    // Layer SDK
    compile project(':sdk')

    // Google Location
    compile 'com.google.android.gms:play-services-location:11.2.0'
    // Image cache and loading
    compile 'com.squareup.picasso:picasso:2.5.2'
    // Image popup
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.4.1'

    // Android UI
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile "android.arch.lifecycle:runtime:1.0.0"
    compile "android.arch.lifecycle:extensions:1.0.0-beta1"

    // Testing
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.8.47'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.linkedin.testbutler:test-butler-library:1.3.1'
    // Espresso support
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

def sdkPom() {
    pom {
        project {
            name = pomProjectName
            packaging "aar"
            artifactId = artifactIdName

            developers {
                developer {
                    name 'Amar Srinivasan'
                    email 'amar@layer.com'
                }
                developer {
                    name 'Peter Elliott'
                    email 'peter@layer.com'
                }
            }
        }
    }
}

public String url() {
    return "file://$rootDir/releases"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    if (JavaVersion.current().isJava8Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

task publishAar(type: Upload, description: "Upload android AAR library") {
    configuration = configurations.archives
    uploadDescriptor = true
    repositories {
        mavenDeployer {
            repository(url: url())
            pom = sdkPom()
        }
    }
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

// Tasks for publishing a release to GitHub

def getReleaseNotes(version) {
    def capturing = false
    def sb = new StringBuilder();
    new File('./CHANGELOG.md').eachLine { line ->
        if (line ==~ /^\#\#\s${Pattern.quote(version)}$/) {
            capturing = true
        } else if (line ==~ /^\#\#\s[\d\.]+$/) {
            capturing = false
        } else if (capturing) {
            sb.append(line + "\n")
        }
    }

    // Trim and escape backticks
    return sb.toString()
            .trim()
            .replaceAll("`", "\\\\`")
}

def getGitHubToken() {
    return hasProperty('GITHUB_TOKEN') ? GITHUB_TOKEN : "";
}

def getUploadCommand(filename) {
    return "tools/github-release upload " +
            "--security-token ${getGitHubToken()} " +
            "--user $org " +
            "--repo $repo " +
            "--tag $version " +
            "--name $filename " +
            "--file releases/$groupFolder/$artifactIdName/$version/$filename"
}

afterEvaluate { project ->
    // Only add publishing tasks if preconditions are met so this can be used as a submodule
    if (!getGitHubToken().isEmpty() && new File('./CHANGELOG.md').exists()) {
        task uploadReleaseNotes(type: Exec) {
            workingDir "$rootDir"
            commandLine "bash", "-c", "tools/github-release release " +
                    "--security-token ${getGitHubToken()} " +
                    "--user $org " +
                    "--repo $repo " +
                    "--tag $version " +
                    "--description \"${getReleaseNotes("$version")}\""
        }

        task uploadReleaseAar(type: Exec, dependsOn: [uploadReleaseNotes, publishAar]) {
            workingDir "$rootDir"

            def filename = "$artifactIdName-${version}.aar"
            commandLine "bash", "-c", getUploadCommand(filename)
        }

        task uploadReleaseSources(type: Exec, dependsOn: [uploadReleaseNotes, publishAar]) {
            workingDir "$rootDir"

            def filename = "$artifactIdName-${version}-sources.jar"
            commandLine "bash", "-c", getUploadCommand(filename)
        }

        task uploadReleaseJavadoc(type: Exec, dependsOn: [uploadReleaseNotes, publishAar]) {
            workingDir "$rootDir"

            def filename = "$artifactIdName-${version}-javadoc.jar"
            commandLine "bash", "-c", getUploadCommand(filename)
        }

        task publishReleaseToGitHub(dependsOn: [uploadReleaseNotes, uploadReleaseAar, uploadReleaseSources, uploadReleaseJavadoc])
    }
}